plugins {
    id 'java'
    id 'jacoco'

    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

group 'de.uniks'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.16-alpha'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: 'jdk-12.0.1+2'

    // https://mvnrepository.com/artifact/org.openjfx/javafx-base
    implementation group: 'org.openjfx', name: 'javafx-base', version: '19-ea+5'

    // https://mvnrepository.com/artifact/org.openjfx/javafx-controls
    implementation group: 'org.openjfx', name: 'javafx-controls', version: '19-ea+5'


    // -------------------- Degger (Dependency Injetion) -------------------------
    implementation 'com.google.dagger:dagger:2.35.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.35.1'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.35.1'
}

javafx {
    version = '17'
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml' ]
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports.xml.enabled = true
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        showStackTraces = true
    }

    systemProperties = [
            'java.awt.headless': 'true',
            'testfx.robot'     : 'glass',
            'testfx.headless'  : 'true',
            'glass.platform'   : 'Monocle',
            'monocle.platform' : 'Headless',
            'prism.order'      : 'sw',
            'prism.text'       : 't2k',
    ]

    jvmArgs += [
            '--add-opens', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
            '--add-opens', 'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED',
    ]
}
